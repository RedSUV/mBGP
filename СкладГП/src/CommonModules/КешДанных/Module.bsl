Процедура Поместить(Тип = "", ИдентификаторПУ, ОбъектТекущейИБ) Экспорт
	
	УстановитьПривилегированныйРежим(ИСТИНА);
	
	Менеджер = РегистрыСведений.КешДанных.СоздатьМенеджерЗаписи();
	Менеджер.БазаПУ = ОбщегоНазначенияВызовСервераПовтИсп.БазаПУ();
	Менеджер.Тип = Тип;
	Менеджер.ИдентификаторПУ = ИдентификаторПУ;
	Менеджер.ОбъектТекущейИБ = ОбъектТекущейИБ;
	Менеджер.Записать(ИСТИНА);
	
КонецПроцедуры

Функция ПолучитьОбъектТекущей(Тип, ИдентификаторПУ) Экспорт
	Выборка = РегистрыСведений.КешДанных.Выбрать(Новый Структура("БазаПУ,Тип,ИдентификаторПУ", ОбщегоНазначенияВызовСервераПовтИсп.БазаПУ(), Тип, ИдентификаторПУ));
	Если Выборка.Следующий() Тогда
		Возврат Выборка.ОбъектТекущейИБ
	Иначе
		Возврат Неопределено
	КонецЕсли
КонецФункции

Функция ПолучитьИдентификаторПУ(Тип, ОбъектТекущейИБ) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	КешДанных.ИдентификаторПУ КАК ИдентификаторПУ
	|ИЗ
	|	РегистрСведений.КешДанных КАК КешДанных
	|ГДЕ
	|	КешДанных.БазаПУ = &БазаПУ
	|	И КешДанных.Тип = &Тип
	|	И КешДанных.ОбъектТекущейИБ = &ОбъектТекущейИБ";
	Запрос.УстановитьПараметр("БазаПУ", ОбщегоНазначенияВызовСервераПовтИсп.БазаПУ());
	Запрос.УстановитьПараметр("Тип", Тип);
	Запрос.УстановитьПараметр("ОбъектТекущейИБ", ОбъектТекущейИБ);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат Неопределено	
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.ИдентификаторПУ;
	КонецЕсли;
	
КонецФункции