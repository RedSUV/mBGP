#Область СлужебныйПрограммныйИнтерфейс

Асинх Процедура ОбработчикИзмененияМестоположения(ИмяПровайдера, ДанныеМестоположения = Неопределено, ДопПар = Неопределено) Экспорт 
	#Если МобильноеПриложениеКлиент или МобильныйКлиент Тогда 
		Если ДанныеМестоположения = Неопределено Тогда 
			Если СредстваГеопозиционирования.ОбновитьМестоположение(ИмяПровайдера, 60) Тогда
				ДанныеМестоположения = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(ИмяПровайдера);
			КонецЕсли;
		КонецЕсли; 
		Если ДанныеМестоположения = Неопределено Тогда
			Возврат 
		КонецЕсли;
		ОбновитьДанныеГеоПозиции(ДанныеМестоположения);
		Если Не ОбщегоНазначенияВызовСервераПовтИсп.ОтключитьЗаписьТрека() Тогда
			ГеопозиционированиеВызовСервера.ЗаписатьДанныеГеоПозицииВТрек(ДанныеГеоПозиции);
		КонецЕсли;
		Оповестить("ИзменениеМестоположения", ДанныеГеоПозиции);
	#КонецЕсли
КонецПроцедуры

Процедура ДанныеГеоПозиции_Инициализация() Экспорт
	ДанныеГеоПозиции = Новый Структура("ТекущееМестоположение,Скорость,Дата", "-", 0, ТекущаяДата());
КонецПроцедуры

Процедура ПодключитьОбработчикИзмененияДоступностиПровайдеров() Экспорт
#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда
	Если НЕ СредстваГеопозиционирования.ИспользованиеДанныхГеопозиционированияВключено() Тогда
		Возврат;	
	КонецЕсли;
	СредстваГеопозиционирования.ПодключитьОбработчикИзмененияДоступностиПровайдеров(
		"ОбработчикИзмененияДоступностиПровайдеров");
#КонецЕсли
КонецПроцедуры

Процедура ОбработчикИзмененияДоступностиПровайдеров(ИмяПровайдера, Доступен, ДополнительныйПараметр) Экспорт
	Если НЕ ЛучшийПровайдерГЕО = Неопределено Тогда
		Если Доступен = ЛОЖЬ и ЛучшийПровайдерГЕО.Имя = ИмяПровайдера Тогда
			ЛучшийПровайдерГЕО = Неопределено;
			#Если МобильноеПриложениеКлиент или МобильныйКлиент Тогда
			СредстваГеопозиционирования.ОтключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения", ИмяПровайдера);
			#КонецЕсли
		КонецЕсли;
	КонецЕсли;
	
	Если Доступен = ЛОЖЬ и ЛучшийПровайдерГЕО <> Неопределено и ЛучшийПровайдерГЕО.Имя = ИмяПровайдера Тогда
		ЛучшийПровайдерГЕО = Неопределено;
	КонецЕсли;
	ОпределитьЛучшегоПровайдераГЕО();
КонецПроцедуры

Асинх Процедура ОпределитьЛучшегоПровайдераГЕО() Экспорт
	#Если МобильноеПриложениеКлиент или МобильныйКлиент Тогда
		Если НЕ СредстваГеопозиционирования.ИспользованиеДанныхГеопозиционированияВключено() Тогда
			ЛучшийПровайдерГЕО = Неопределено;
			Возврат
		КонецЕсли;
		
		ВсеПровайдеры = СредстваГеопозиционирования.ПолучитьПровайдеров(ИСТИНА); 
		Если ВсеПровайдеры = Неопределено Тогда
			 Возврат 
		КонецЕсли;
		СамыйТочныйПровайдер = Неопределено;
		Для Каждого Провайдер из ВсеПровайдеры Цикл
			Если СамыйТочныйПровайдер = Неопределено Тогда
				СамыйТочныйПровайдер = Провайдер;
				Продолжить;	
			КонецЕсли;
			Если СамыйТочныйПровайдер.Точность > Провайдер.Точность Тогда
				СамыйТочныйПровайдер = Провайдер;
			КонецЕсли;
		КонецЦикла;
		
		Если ЛучшийПровайдерГЕО = СамыйТочныйПровайдер  или СамыйТочныйПровайдер = Неопределено Тогда
			Возврат
		Иначе
			ЛучшийПровайдерГЕО = СамыйТочныйПровайдер;
		КонецЕсли;
		
		СредстваГеопозиционирования.ОтключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения", ЛучшийПровайдерГЕО.Имя);
		
		ЛучшийПровайдерГЕО = СамыйТочныйПровайдер;
		СредстваГеопозиционирования.ПодключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения", ЛучшийПровайдерГЕО.Имя, 5, 10);
		ОбработчикИзмененияМестоположения(ЛучшийПровайдерГЕО.Имя);
		
	#КонецЕсли 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьДанныеГеоПозиции(ДанныеМестоположения) 
	Если ДанныеГеоПозиции = Неопределено Тогда
		ДанныеГеоПозиции_Инициализация()
	КонецЕсли;
	
	ДанныеГеоПозиции.ТекущееМестоположение = СтрШаблон("%1, %2", ДанныеМестоположения.Координаты.Широта, ДанныеМестоположения.Координаты.Долгота);
	ДанныеГеоПозиции.Скорость = ДанныеМестоположения.Скорость;
	ДанныеГеоПозиции.Дата = ДанныеМестоположения.Дата;
КонецПроцедуры

Процедура ФоноваяАрхивацияТекущегоТрека()
		
КонецПроцедуры

#КонецОбласти