#Область СлужебныйПрограммныйИнтерфейс

Асинх Процедура ОбработчикИзмененияМестоположения(ИмяПровайдера, ДанныеМестоположения = Неопределено, ДопПар = Неопределено) Экспорт 
	#Если МобильноеПриложениеКлиент или МобильныйКлиент Тогда 
		Если ДанныеМестоположения = Неопределено Тогда 
			Если СредстваГеопозиционирования.ОбновитьМестоположение(ИмяПровайдера, 60) Тогда
				ДанныеМестоположения = СредстваГеопозиционирования.ПолучитьПоследнееМестоположение(ИмяПровайдера);
			КонецЕсли;
		КонецЕсли; 
		Если ДанныеМестоположения = Неопределено Тогда
			Возврат 
		КонецЕсли;
		ОбновитьДанныеГеоПозиции(ДанныеМестоположения);
		Если Не ОбщегоНазначенияВызовСервераПовтИсп.ОтключитьЗаписьТрека() Тогда
			ГеопозиционированиеВызовСервера.ЗаписатьДанныеГеоПозицииВТрек(ДанныеГеоПозиции);
		КонецЕсли;
		Оповестить("ИзменениеМестоположения", ДанныеГеоПозиции);
	#КонецЕсли
КонецПроцедуры

// Данные гео позиции инициализация.
Процедура ДанныеГеоПозиции_Инициализация() Экспорт
	ДанныеГеоПозиции = Новый Структура("ТекущееМестоположение,Скорость,Дата", "-", 0, ТекущаяДата());
КонецПроцедуры

Процедура ПодключитьОбработчикИзмененияДоступностиПровайдеров() Экспорт
#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда
	СредстваГеопозиционирования.ПодключитьОбработчикИзмененияДоступностиПровайдеров(
		"ОбработчикИзмененияДоступностиПровайдеров");
#КонецЕсли
КонецПроцедуры

Асинх Процедура ОбработчикИзмененияДоступностиПровайдеров(ИмяПровайдера, Доступен, ДополнительныйПараметр) Экспорт
	#Если МобильноеПриложениеКлиент Или МобильныйКлиент Тогда
		ЛучшийПровайдерГЕО = СредстваГеопозиционирования.ПолучитьСамогоЭнергоЭкономичногоПровайдера(ИСТИНА);
		
		Если ЛучшийПровайдерГЕО = Неопределено Тогда
			СредстваГеопозиционирования.ОтключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения");
		Иначе
			СредстваГеопозиционирования.ПодключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения", ЛучшийПровайдерГЕО, 5, 10);
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

Асинх Процедура ПриСтартеСистемы() Экспорт
	//Подключаем обработчик ОбработчикИзмененияМестоположения и при изменении геоположения
	#Если МобильноеПриложениеКлиент или МобильныйКлиент Тогда
		Если НЕ СредстваГеопозиционирования.ИспользованиеДанныхГеопозиционированияВключено() Тогда
			Возврат
		КонецЕсли;
		ПодключитьОбработчикИзмененияДоступностиПровайдеров();
		ДанныеГеоПозиции_Инициализация();
	#КонецЕсли 
КонецПроцедуры

Асинх Процедура ОпределитьЛучшегоПровайдераГЕО() Экспорт
	#Если МобильноеПриложениеКлиент или МобильныйКлиент Тогда
		Если НЕ СредстваГеопозиционирования.ИспользованиеДанныхГеопозиционированияВключено() Тогда
			СредстваГеопозиционирования.ОтключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения");
			ЛучшийПровайдерГЕО = Неопределено;
			Возврат
		КонецЕсли;
		
		ВсеПровайдеры = СредстваГеопозиционирования.ПолучитьПровайдеров(ИСТИНА); 
		Если ВсеПровайдеры = Неопределено Тогда
			 Возврат 
		КонецЕсли;
		СамыйТочныйПровайдер = Неопределено;
		Для Каждого Провайдер из ВсеПровайдеры Цикл
			Если СамыйТочныйПровайдер = Неопределено Тогда
				СамыйТочныйПровайдер = Провайдер;
				Продолжить;	
			КонецЕсли;
			Если СамыйТочныйПровайдер.Точность > Провайдер.Точность Тогда
				СамыйТочныйПровайдер = Провайдер;
			КонецЕсли;
		КонецЦикла;
		
		Если ЛучшийПровайдерГЕО = СамыйТочныйПровайдер  или СамыйТочныйПровайдер = Неопределено Тогда
			Возврат
		Иначе
			ЛучшийПровайдерГЕО = СамыйТочныйПровайдер;
		КонецЕсли;
		
		СредстваГеопозиционирования.ОтключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения", ЛучшийПровайдерГЕО.Имя);
		
		ЛучшийПровайдерГЕО = СамыйТочныйПровайдер;
		СредстваГеопозиционирования.ПодключитьОбработчикИзмененияМестоположения("ОбработчикИзмененияМестоположения", ЛучшийПровайдерГЕО.Имя, 5, 10);
		ОбработчикИзмененияМестоположения(ЛучшийПровайдерГЕО.Имя);
		
	#КонецЕсли 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьДанныеГеоПозиции(ДанныеМестоположения) 
	Если ДанныеГеоПозиции = Неопределено Тогда
		ДанныеГеоПозиции_Инициализация()
	КонецЕсли;
	
	ДанныеГеоПозиции.ТекущееМестоположение = СтрШаблон("%1, %2", ДанныеМестоположения.Координаты.Широта, ДанныеМестоположения.Координаты.Долгота);
	ДанныеГеоПозиции.Скорость = ДанныеМестоположения.Скорость;
	ДанныеГеоПозиции.Дата = ДанныеМестоположения.Дата;
КонецПроцедуры

Процедура ФоноваяАрхивацияТекущегоТрека()
		
КонецПроцедуры

#КонецОбласти