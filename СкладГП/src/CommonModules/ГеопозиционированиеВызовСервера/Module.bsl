Процедура ЗаписатьДанныеГеоПозицииВТрек(Знач ДанныеГеоПозиции) Экспорт 
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТекущийТрек.Дата КАК Дата,
	|	ТекущийТрек.id КАК id
	|ИЗ
	|	РегистрСведений.ТекущийТрек КАК ТекущийТрек";
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда 
		ИдентификаторТочки = Новый УникальныйИдентификатор;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		ИдентификаторТочки = Выборка.id
	КонецЕсли;
	МенеджерЗаписиРегистра = РегистрыСведений.ТекущийТрек.СоздатьМенеджерЗаписи();
	МенеджерЗаписиРегистра.id = ИдентификаторТочки;
	ЗаполнитьЗначенияСвойств(МенеджерЗаписиРегистра, ДанныеГеоПозиции); 
	МенеджерЗаписиРегистра.Записать(ИСТИНА);
КонецПроцедуры 

Процедура АрхивацияТекущегоТрека() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТекущийТрек.id КАК id,
	|	МИНИМУМ(ТекущийТрек.Дата) КАК Начало,
	|	МАКСИМУМ(ТекущийТрек.Дата) КАК Окончание
	|ИЗ
	|	РегистрСведений.ТекущийТрек КАК ТекущийТрек
	|
	|СГРУППИРОВАТЬ ПО
	|	ТекущийТрек.id
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТекущийТрек.id КАК id,
	|	ТекущийТрек.Дата КАК Дата,
	|	ТекущийТрек.ТекущееМестоположение КАК ТекущееМестоположение,
	|	ТекущийТрек.Скорость КАК Скорость
	|ИЗ
	|	РегистрСведений.ТекущийТрек КАК ТекущийТрек"; 
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат.Получить(0).Выбрать();
	Если Выборка.Следующий() Тогда 
		МенеджерЗаписиРегистра = РегистрыСведений.Треки.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиРегистра, Выборка);
		МенеджерЗаписиРегистра.ДанныеТрека = Новый ХранилищеЗначения(ОбщегоНазначения.ТаблицаЗначенийВМассив(Результат.Получить(1).Выгрузить()), Новый СжатиеДанных(7));
		МенеджерЗаписиРегистра.Записать(); 
		//Чистим
		НаборЧистим = РегистрыСведений.ТекущийТрек.СоздатьНаборЗаписей();
		НаборЧистим.Записать(ИСТИНА);
	КонецЕсли;
	
КонецПроцедуры

Процедура ФоноваяАрхивацияТекущегоТрека() Экспорт
	ФоновыеЗадания.Выполнить("ГеопозиционированиеВызовСервера.АрхивацияТекущегоТрека")
КонецПроцедуры	

