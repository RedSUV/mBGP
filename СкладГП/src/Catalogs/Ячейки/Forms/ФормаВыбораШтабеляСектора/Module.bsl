&НаСервере
Процедура ЗаполнитьТаблицуШтабелей()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Ячейки.Ссылка КАК Ссылка,
	|	Ячейки.Наименование КАК Наименование,
	|	ЕСТЬNULL(ВложенныйЗапрос.Пакетов, 0) КАК Пакетов
	|ИЗ
	|	Константа.Склад КАК КонстСклад
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Ячейки КАК Ячейки
	|			ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|				КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ОстаткиПакетовНаСкладах.НомерПакета) КАК Пакетов,
	|				ОстаткиПакетовНаСкладах.Ячейка КАК Ячейка
	|			ИЗ
	|				Константа.Склад КАК КонстантаСклад
	|					ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ОстаткиПакетовНаСкладах КАК ОстаткиПакетовНаСкладах
	|					ПО КонстантаСклад.Значение = ОстаткиПакетовНаСкладах.Склад
	|			ГДЕ
	|				ОстаткиПакетовНаСкладах.Ряд > 0
	|			
	|			СГРУППИРОВАТЬ ПО
	|				ОстаткиПакетовНаСкладах.Ячейка) КАК ВложенныйЗапрос
	|			ПО Ячейки.Ссылка = ВложенныйЗапрос.Ячейка
	|		ПО КонстСклад.Значение = Ячейки.Владелец
	|ГДЕ
	|	НЕ Ячейки.ЭтоГруппа
	|	И Ячейки.Родитель = &Родитель
	|	И НЕ Ячейки.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ячейки.НомерВСекторе,
	|	Наименование";
	Заголовок = "Выбор штабеля сектора "+Параметры.Сектор;
	Запрос.УстановитьПараметр("Родитель", Параметры.Сектор);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СтрокаТаблицы = ТаблицаШтабелей.Добавить();
		СтрокаТаблицы.Ссылка = Выборка.Ссылка;
		СтрокаТаблицы.Пакетов = Выборка.Пакетов;
		ЭлементФормы = ЯчейкиСклада.ЭлементФормыСекторШтабельКнопка(ЭтотОбъект, Элементы, Неопределено, СтрокаТаблицы.Ссылка, НЕОПРЕДЕЛЕНО, СтрокаТаблицы);
		СтрокаТаблицы.ИмяЭлементаФормы = ЭлементФормы.Имя;
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
КонецПроцедуры 

&НаКлиенте
Процедура НачатьВыводКвадратиковНаФормуПослеОткрытия()
	ОпределитьПараметрыРазмещенияЭлементовПриИзмененииПараметровЭкрана();
	НастроитьФорму(ИСТИНА);	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьПараметрыРазмещенияЭлементовПриИзмененииПараметровЭкрана()
	ДанныеЭкрана = ПолучитьИнформациюЭкрановКлиента();
	Если ДанныеЭкрана[0].Ширина > ДанныеЭкрана[0].Высота Тогда
		ГоризонтальноеПоложениеЭкрана = Истина;
		ЭлементовПоГоризонтали = 6;	
	Иначе
		ГоризонтальноеПоложениеЭкрана = Ложь;
		ЭлементовПоГоризонтали = 3;
	КонецЕсли;
	ШиринаЭкрана = ДанныеЭкрана[0].Ширина;
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	ОпределитьПараметрыРазмещенияЭлементовПриИзмененииПараметровЭкрана();
	ВывестиЯчейкиШтабелейНаФорму();
КонецПроцедуры 

&НаСервере
Процедура ВывестиЯчейкиШтабелейНаФорму()
	ЯчейкиСклада.НастроитьФормуСектораШтабеляКвадратики(ЭтотОбъект, ТаблицаШтабелей, ИСТИНА);	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму(ЗаполнитьТаблицу = ЛОЖЬ)
	Если ЗаполнитьТаблицу Тогда
		ЗаполнитьТаблицуШтабелей()
	КонецЕсли;
	ВывестиЯчейкиШтабелейНаФорму();
КонецПроцедуры

&НаКлиенте
Процедура СекторШтабельНажатие(Элемент, СтандартнаяОбработка)

	СтрокаОписания = ПолучитьСтрокуОписанияЭлементаФормыНаКлиенте(Элемент.Имя, ТаблицаШтабелей);	
	
	ЭтаФорма.Закрыть(СтрокаОписания.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьСтрокуОписанияЭлементаФормыНаКлиенте(ИмяЭлемента, ТаблицаОписания)
	СтрПакета = ТаблицаОписания.НайтиСтроки(Новый Структура("ИмяЭлементаФормы", ИмяЭлемента));
	Если СтрПакета.Количество() = 0 Тогда 
		Возврат Неопределено
	КонецЕсли;
	Возврат СтрПакета[0]
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ПодключитьОбработчикОжидания("НачатьВыводКвадратиковНаФормуПослеОткрытия",0.5, ИСТИНА);
КонецПроцедуры

&НаКлиенте
Процедура СекторШтабельНажатиеКнопки(Команда)
	СтрокаОписания = ПолучитьСтрокуОписанияЭлементаФормыНаКлиенте(Команда.Имя, ТаблицаШтабелей);	
	ЭтаФорма.Закрыть(СтрокаОписания.Ссылка);
КонецПроцедуры
