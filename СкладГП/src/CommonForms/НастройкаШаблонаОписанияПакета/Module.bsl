
&НаКлиенте
Процедура Сохранить(Команда)
	Проверить("");
	Если СтрНайти(РезультатПроверки, "ОШИБКИ") = 0 Тогда
		СохранитьНаСервере();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

Функция ПривестиШаблон()
	Ответ = Новый Структура("Текст, Параметры", "", Новый Массив());
	НачалоПар = 0;
	ПривШаблон  = "";
	Для инд = 1 по СтрДлина(Шаблон) Цикл
		ТекСимвол = Сред(Шаблон,инд,1);
		Если ТекСимвол = "[" Тогда
			ПривШаблон = ПривШаблон + "%"+(Ответ.Параметры.Количество()+1);
			НачалоПар = инд+1;
		ИначеЕсли ТекСимвол = "]" Тогда
			Ответ.Параметры.Добавить(Сред(Шаблон, НачалоПар, инд-НачалоПар));
			НачалоПар = 0;
		Иначе
			Если НачалоПар = 0 Тогда
				ПривШаблон = ПривШаблон + ТекСимвол;	
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
		
	Ответ.Текст = ПривШаблон;
	
	Возврат Ответ;
КонецФУнкции

&НаСервере
Функция ВычислитьШаблон(ДанныеШаблона)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"Выбрать Первые 1
	|"+СтрСоединить(ДанныеШаблона.Параметры,", ")+"
	|ИЗ РегистрСведений.СвойстваПакетов";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	СтрПараметров = "Выборка."+СтрСоединить(ДанныеШаблона.Параметры, ", Выборка.");
	//@skip-check server-execution-safe-mode
	Возврат  Вычислить("СтрШаблон(ДанныеШаблона.Текст,"+СтрПараметров+")"); 
КонецФункции

&НаКлиенте
Процедура Проверить(Команда)
	РезультатПроверки = "";
	ДанныеШаблона = ПривестиШаблон();
	Если ДанныеШаблона.Параметры.Количество() > 0 Тогда
		Попытка
			РезультатПроверки = ВычислитьШаблон(ДанныеШаблона);
		Исключение
			РезультатПроверки = "ОШИБКИ!!";
		КонецПопытки;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ЗаполнитьПодсказки();
	ПрочитатьНаСервере()
КонецПроцедуры

&НаСервере
Процедура СохранитьНаСервере()
	Константы.ШаблонПредставленияПакета.Установить(Шаблон);
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНаСервере()
	Шаблон = Константы.ШаблонПредставленияПакета.Получить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПодсказки()
	ДоступныеСвойства = ДоступныеСвойства();
	ОписаниеДоступныхСвойств = СтрСоединить(ДоступныеСвойства, ", ");
КонецПроцедуры

&НаКлиенте
Функция ДоступныеСвойства()
	ДоступныеСвойства = Новый Массив;
	ДоступныеСвойства.Добавить("Сечение");
	ДоступныеСвойства.Добавить("СечениеТолщина");
	ДоступныеСвойства.Добавить("СечениеШирина");
	ДоступныеСвойства.Добавить("Сорт");
	ДоступныеСвойства.Добавить("ДлинаТ");
	ДоступныеСвойства.Добавить("Влажность");
	ДоступныеСвойства.Добавить("Досок");
	ДоступныеСвойства.Добавить("Объем");
	ДоступныеСвойства.Добавить("ДатаВыпуска");
	ДоступныеСвойства.Добавить("Порода");
	ДоступныеСвойства.Добавить("ВидПродукции");
	Возврат ДоступныеСвойства
КонецФункции

&НаКлиенте
Процедура ПриИзмененииПараметровЭкрана()
	ОбщегоНазначенияКлиент.ЗаполнитьСвойстваФормыПриИзмененииПараметровЭкрана(ЭтаФорма);
КонецПроцедуры
