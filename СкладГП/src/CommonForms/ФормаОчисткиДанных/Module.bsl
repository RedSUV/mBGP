
&НаКлиенте
Процедура Очистить(Команда)
	НачатьОчисткуДанных()
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	НеОчищать = Новый Массив;
	НеОчищать.Добавить(Метаданные.Справочники.БазыПУ);
	НеОчищать.Добавить(Метаданные.Справочники.Склады);
	
	Для Каждого м из Метаданные.РегистрыСведений Цикл
		Если НеОчищать.Найти(м) = Неопределено Тогда
			СписокРегистров.Добавить(м.ПолноеИмя(), м.Синоним, ИСТИНА);
		КонецЕсли;
	КонецЦикла;
	Для Каждого м из Метаданные.Справочники Цикл 
		Если НеОчищать.Найти(м) = Неопределено Тогда
			СписокСправочников.Добавить(м.ПолноеИмя(), м.Синоним, ИСТИНА);
		КонецЕсли;
	КонецЦикла;  
	Для Каждого м из Метаданные.Документы Цикл 
		Если НеОчищать.Найти(м) = Неопределено Тогда
			СписокДокументов.Добавить(м.ПолноеИмя(), м.Синоним, ИСТИНА);
		КонецЕсли;
	КонецЦикла; 
КонецПроцедуры

&НаКлиенте
Процедура СписокРегистровПередУдалением(Элемент, Отказ)
	Отказ = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура СписокСправочниковПередУдалением(Элемент, Отказ)
	Отказ = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура СписокРегистровПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура СписокСправочниковПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Процедура СписокДокументовПередУдалением(Элемент, Отказ)
	Отказ = ИСТИНА;
КонецПроцедуры

&НаКлиенте
Асинх Процедура НачатьОчисткуДанных()
	ОтветНаВопрос = Ждать ВопросАсинх("Очистить данные все сохраненные данные приложения?", РежимДиалогаВопрос.ДаНет);
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		МассивРегистров = Новый Массив;
		Для Каждого м из СписокРегистров Цикл
			Если м.Пометка Тогда
				МассивРегистров.Добавить(м.Значение);
			КонецЕсли;
		КонецЦикла; 
		МассивСправочников = Новый Массив;
		Для Каждого м из СписокСправочников Цикл
			Если м.Пометка Тогда
				МассивСправочников.Добавить(м.Значение);
			КонецЕсли;
		КонецЦикла; 
		МассивДокументов = Новый Массив;
		Для Каждого м из СписокДокументов Цикл
			Если м.Пометка Тогда
				МассивДокументов.Добавить(м.Значение);
			КонецЕсли;
		КонецЦикла; 

		МетаданныеКУдалению = Новый Структура("МассивРегистров,МассивСправочников,МассивДокументов", МассивРегистров,МассивСправочников,МассивДокументов);
		АдресРезультата = ПоместитьВоВременноеХранилище(НЕОПРЕДЕЛЕНО, Новый УникальныйИдентификатор);
		ОповещениеПослеОчистки = Новый ОписаниеОповещения("ПослеЗакрытияФормыОчисткиДанных", СлужебныйОбновлениеКлиент, АдресРезультата);
		ПараметрыДЗ = Новый Структура;
		ПараметрыДЗ.Вставить("МетаданныеКУдалению", МетаданныеКУдалению);
		ПараметрыДЗ.Вставить("АдресРезультата", АдресРезультата);
 		ДлительныеОперацииКлиент.ВыполнитьФоново("СлужебныйОбновление.ОчиститьДанныеНаСервере", ПараметрыДЗ, "Очистка данных", ОповещениеПослеОчистки, ИСТИНА);
	КонецЕсли;
	
КонецПроцедуры

