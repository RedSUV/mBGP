Процедура ИзменитьРасположениеПакета(Пакет, Ячейка, Ряд, Уровень, ОбменДаннымиЗагрузка = ЛОЖЬ, ЕстьСвязь = ЛОЖЬ) Экспорт
	УстановитьПривилегированныйРежим(ИСТИНА);

	НЗ = РегистрыСведений.ОстаткиПакетовНаСкладах.СоздатьНаборЗаписей(); 
	
	НЗ.ДополнительныеСвойства.Вставить("ИзмененияНеВыгружатьСразу", Не ЕстьСвязь);
	Если Не ЕстьСвязь Тогда
		НЗ.ДополнительныеСвойства.Вставить("ЕстьСвязь", ЕстьСвязь);	
	КонецЕсли;
	
	НЗ.ОбменДанными.Загрузка = ОбменДаннымиЗагрузка;  
	НЗ.Отбор.НомерПакета.Установить(Пакет);   
	НЗ.Прочитать();
	Если НЗ.Количество() > 0 Тогда
		СтрПакета = НЗ[0]
	Иначе
		СтрПакета = НЗ.Добавить(); 
		Склад = Ячейка.Склад;
		Если Не ЗначениеЗаполнено(Склад) Тогда
			Склад = ОбщегоНазначенияВызовСервераПовтИсп.Склад();
		КонецЕсли;
		СтрПакета.Склад = Склад
	КонецЕсли; 
	СтрПакета.НомерПакета = Пакет;
	СтрПакета.Ячейка = Ячейка;
	СтрПакета.Ряд = Ряд;
	СтрПакета.Уровень = Уровень;   
	
	СтрПакета.ВремяСобытия = ТекущаяДата();
	
	НЗ.Записать(ИСТИНА);

КонецПроцедуры 

Процедура ИзменитьРасположениеПакетов(МассивРазмещений, ОбменДаннымиЗагрузка = ЛОЖЬ, ЕстьСвязь = ИСТИНА) Экспорт
	УстановитьПривилегированныйРежим(ИСТИНА);
	ТаблицаРазмещения = ПодготовитьТаблицуРазмещенияПакетов(МассивРазмещений);
	
	Для Каждого стр из ТаблицаРазмещения Цикл  
		ИзменитьРасположениеПакета(стр.НомерПакета, стр.Ячейка, стр.Ряд, стр.Уровень, ОбменДаннымиЗагрузка, ЕстьСвязь);		
	КонецЦикла;

КонецПроцедуры

Функция ПодготовитьТаблицуРазмещенияПакетов(МассивРазмещений)
	МассивПакетов = Новый Массив;
	Для Каждого стр из МассивРазмещений Цикл 
		Если стр.Свойство("Пакет") Тогда
			МассивПакетов.Добавить(стр.Пакет);
		Иначе 
			МассивПакетов.Добавить(стр.НомерПакета);
		КонецЕсли;
	КонецЦикла;                           
	УстановитьПривилегированныйРежим(ИСТИНА);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ОстаткиПакетовНаСкладах.Склад КАК Склад,
	|	ОстаткиПакетовНаСкладах.НомерПакета КАК НомерПакета,
	|	ОстаткиПакетовНаСкладах.Объем КАК Объем,
	|	ОстаткиПакетовНаСкладах.Досок КАК Досок,
	|	ОстаткиПакетовНаСкладах.Ячейка КАК Ячейка,
	|	ОстаткиПакетовНаСкладах.Ряд КАК Ряд,
	|	ОстаткиПакетовНаСкладах.Уровень КАК Уровень,
	|	ОстаткиПакетовНаСкладах.Номер КАК Номер
	|ИЗ
	|	РегистрСведений.ОстаткиПакетовНаСкладах КАК ОстаткиПакетовНаСкладах
	|ГДЕ
	|	ОстаткиПакетовНаСкладах.Склад В (&Склады)
	|	И ОстаткиПакетовНаСкладах.НомерПакета В(&МассивПакетов)";
	Запрос.УстановитьПараметр("Склады", ОбщегоНазначенияВызовСервераПовтИсп.МассивСкладов());
	Запрос.УстановитьПараметр("МассивПакетов", МассивПакетов);
	ТаблицаРазмещения = Запрос.Выполнить().Выгрузить();
	Для Каждого стр из МассивРазмещений Цикл
		Если стр.Свойство("Пакет") Тогда
			Отб = Новый Структура("НомерПакета,Ячейка,Ряд,Уровень", стр.Пакет, стр.Ячейка, стр.Ряд, стр.Уровень);
		Иначе 
			Отб = Новый Структура("НомерПакета,Ячейка,Ряд,Уровень", стр.НомерПакета, стр.Ячейка, стр.Ряд, стр.Уровень);
		КонецЕсли;

		СтрокиРазмещения = ТаблицаРазмещения.НайтиСтроки(Отб);
		Если СтрокиРазмещения.Количество() = 0 Тогда
			СтрокаРазмещения = ТаблицаРазмещения.Добавить(); 
			Если Не стр.Свойство("Склад") Тогда
				стр.Вставить("Склад", стр.Ячейка.Склад);	
			КонецЕсли;
			
			СтрокаРазмещения.Склад = стр.Склад;
		Иначе 
			СтрокаРазмещения = СтрокиРазмещения[0]
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(СтрокаРазмещения, Отб);
	КонецЦикла;
	Возврат ТаблицаРазмещения;	
КонецФункции

